buildscript {
	repositories {
		jcenter()
		maven {
			name "forge"
			url "http://files.minecraftforge.net/maven/"
		}
	}
	dependencies {
		classpath "net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT"
	}
}

plugins {
	id "com.matthewprenger.cursegradle" version "1.0.6"
}

apply plugin: "net.minecraftforge.gradle.forge"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

version = mc_version + "-" + mod_version

minecraft {
	version = mc_version + "-" + forge_version
	mappings = mcp_mappings
	runDir = "run"
}

repositories {
	jcenter()
	maven {
		name "shadowfacts"
		url "http://mvn.rx14.co.uk/shadowfacts/"
	}
	maven {
		name "ProfMobius"
		url "http://mobiusstrip.eu/maven/"
	}
	maven {
		name "OpenComputers"
		url "http://maven.cil.li/"
	}
}

dependencies {
	deobfCompile group: "net.shadowfacts", name: "ShadowMC", version: mc_version + "-" + shadowmc_version

	deobfCompile group: "mcp.mobius.waila", name: "Waila", version: waila_version + "_" + mc_version
	deobfCompile group: "li.cil.oc", name: "OpenComputers", version: "MC" + mc_version + "-" + oc_version
}

processResources {
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version

	from (sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'
		expand 'version':project.version, 'mcversion':project.minecraft.version
	}
	from (sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
}

curseforge {
	apiKey = project.hasProperty('curseForgeApiKey') ? project.curseForgeApiKey : ''

	project {
		id = "236923"

		addGameVersion "1.9.4"
		addGameVersion "Java 8"

		mainArtifact(jar) {
			releaseType = "beta"
			changelog = ""

			relations {
				requiredLibrary "shadowmc"
			}
		}
	}
}